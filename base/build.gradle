apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

//noinspection GroovyUnusedAssignment
targetCompatibility = '1.7'
sourceCompatibility = '1.7'


repositories {
    mavenCentral()
}

ext {
    forge_base_version = '4.2.0'
}

group = 'com.bolyartech.forge'
version = forge_base_version

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar//, javadocJar
}

apply from: 'bintray-config.gradle'

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.bolyartech.forge'
            artifactId 'base'
            version forge_base_version
            artifact sourcesJar
        }
    }
}


bintray {
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'forge-base'
        userOrg = 'ogibankov'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/ogrebgr/forge'
        issueTrackerUrl = 'https://github.com/ogrebgr/forge/issues'
        vcsUrl = 'https://github.com/ogrebgr/forge.git'
        version {
            name = forge_base_version
            desc = 'Forge base'
            vcsTag = 'v' + forge_base_version
        }
    }
    publications = ['MyPublication']
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'com.darwinsys:hirondelle-date4j:1.5.1'
    compile 'com.google.guava:guava:20.0'
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile 'org.slf4j:slf4j-nop:1.7.7'
    /* if you need logging during test - comment slf4j-nop and uncomment logback-classic
    testCompile 'ch.qos.logback:logback-classic:0.9.26'
    */

    compile 'javax.inject:javax.inject:1'
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
}

